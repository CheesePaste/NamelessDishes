plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
}


version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

//println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

minecraft {
    mappings channel: mapping_channel, version: mapping_version
    copyIdeResources = true
}

apply from: 'gradle/runs.gradle'
apply from: 'gradle/mixin.gradle'
apply from: 'gradle/publishing-local.gradle'

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

    maven { url "https://maven.blamejared.com" }
}
apply from: 'gradle/optional-dep.gradle'

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    annotationProcessor("org.spongepowered:mixin:${mixin_version}:processor")

    annotationProcessor "io.github.llamalad7:mixinextras-common:0.4.1"
    compileOnly       "io.github.llamalad7:mixinextras-common:0.4.1"
    implementation    "io.github.llamalad7:mixinextras-forge:0.4.1"
    jarJar            "io.github.llamalad7:mixinextras-forge:[0.4.1,)"

    implementation fg.deobf("curse.maven:farmers-delight-398521:4679319")

    implementation fg.deobf("curse.maven:jei-238222:7391695")

    // JUnit 5 for testing
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
    testImplementation 'org.mockito:mockito-core:5.7.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.7.0'
}


def modProps = [
        minecraft_version      : minecraft_version,
        minecraft_version_range: minecraft_version_range,
        forge_version          : forge_version,
        forge_version_range    : forge_version_range,
        loader_version_range   : loader_version_range,
        mod_id                 : mod_id,
        mod_name               : mod_name,
        mod_license            : mod_license,
        mod_version            : mod_version,
        mod_authors            : mod_authors,
        mod_description        : mod_description,
]

tasks.named('processResources', ProcessResources).configure {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    inputs.properties(modProps)

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand modProps + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'    : modProps.mod_id,
                'Implementation-Title'   : project.name,
                'Implementation-Version' : project.jar.archiveVersion,
                'Implementation-Vendor'  : modProps.mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'MixinConfigs'           : "${modProps.mod_id}.mixins.json"
        ])
    }
    finalizedBy 'reobfJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

jarJar.enable()
reobf {
    create("jarJar")
}
tasks.named('jarJar').configure { finalizedBy 'reobfJarJar' }

// Configure JUnit 5 tests
test {
    useJUnitPlatform()
}